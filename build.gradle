apply plugin: 'java'
apply plugin: 'eclipse'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

jar {
    doFirst {
        def serviceDir = file("$buildDir/META-INF/services")
        serviceDir.deleteDir()
        serviceDir.mkdirs()

        // copy all service files from deps to buildDir
        for(file in configurations.runtime) {
            zipTree(file).matching{ include 'META-INF/services/*' }.each { f ->
                new File(serviceDir, f.name) << f.getText("UTF-8")
            }
        }
    }

    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Main-Class': 'com.qe.QueryEngineApplication'
                   // etc ...
    }

    from { (configurations.runtime.collect{ it.isDirectory() ? it : zipTree(it) } ) } {
        exclude 'META-INF/**' // Don't let Gradle merge service files
    }

    // include service files from the buildDir
    from fileTree(buildDir).matching{ include 'META-INF/services/*' }
}

dependencies {

    compile 'io.dropwizard:dropwizard-core:0.9.1'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'com.google.guava:guava:19.0'
    compile 'com.netflix.hystrix:hystrix-core:1.4.21'
	compile 'org.springframework:spring-context:4.2.4.RELEASE'
	compile 'org.springframework:spring-context-support:4.2.4.RELEASE'
	
	
    testCompile 'junit:junit:4.11'
}
